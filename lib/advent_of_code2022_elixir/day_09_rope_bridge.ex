defmodule AdventOfCode2022Elixir.Day09RopeBridge do
  @moduledoc """
  --- Day 9: Rope Bridge ---

  This rope bridge creaks as you walk along it. You aren't sure how old it is, or whether it can even support your weight.

  It seems to support the Elves just fine, though. The bridge spans a gorge which was carved out by the massive river far below you.

  You step carefully; as you do, the ropes stretch and twist. You decide to distract yourself by modeling rope physics; maybe you can even figure out where not to step.

  Consider a rope with a knot at each end; these knots mark the head and the tail of the rope. If the head moves far enough away from the tail, the tail is pulled toward the head.

  Due to nebulous reasoning involving Planck lengths, you should be able to model the positions of the knots on a two-dimensional grid. Then, by following a hypothetical series of motions (your puzzle input) for the head, you can determine how the tail will move.

  Due to the aforementioned Planck lengths, the rope must be quite short; in fact, the head (H) and tail (T) must always be touching diagonally adjacent and even overlapping both count as touching):

  ....
  .TH.
  ....

  ....
  .H..
  ..T.
  ....

  ...
  .H. (H covers T)
  ...

  If the head is ever two steps directly up, down, left, or right from the tail, the tail must also move one step in that direction so it remains close enough:

  .....    .....    .....
  .TH.. -> .T.H. -> ..TH.
  .....    .....    .....

  ...    ...    ...
  .T.    .T.    ...
  .H. -> ... -> .T.
  ...    .H.    .H.
  ...    ...    ...

  Otherwise, if the head and tail aren't touching and aren't in the same row or column, the tail always moves one step diagonally to keep up:

  .....    .....    .....
  .....    ..H..    ..H..
  ..H.. -> ..... -> ..T..
  .T...    .T...    .....
  .....    .....    .....

  .....    .....    .....
  .....    .....    .....
  ..H.. -> ...H. -> ..TH.
  .T...    .T...    .....
  .....    .....    .....

  You just need to work out where the tail goes as the head follows a series of motions. Assume the head and the tail both start at the same position, overlapping.

  For example:

  R 4
  U 4
  L 3
  D 1
  R 4
  D 1
  L 5
  R 2

  This series of motions moves the head right four steps, then up four steps, then left three steps, then down one step, and so on. After each step, you'll need to update the position of the tail if the step means the head is no longer adjacent to the tail. Visually, these motions occur as follows (s marks the starting position as a reference point):

  == Initial State ==

  ......
  ......
  ......
  ......
  H.....  (H covers T, s)

  == R 4 ==

  ......
  ......
  ......
  ......
  TH....  (T covers s)

  ......
  ......
  ......
  ......
  sTH...

  ......
  ......
  ......
  ......
  s.TH..

  ......
  ......
  ......
  ......
  s..TH.

  == U 4 ==

  ......
  ......
  ......
  ....H.
  s..T..

  ......
  ......
  ....H.
  ....T.
  s.....

  ......
  ....H.
  ....T.
  ......
  s.....

  ....H.
  ....T.
  ......
  ......
  s.....

  == L 3 ==

  ...H..
  ....T.
  ......
  ......
  s.....

  ..HT..
  ......
  ......
  ......
  s.....

  .HT...
  ......
  ......
  ......
  s.....

  == D 1 ==

  ..T...
  .H....
  ......
  ......
  s.....

  == R 4 ==

  ..T...
  ..H...
  ......
  ......
  s.....

  ..T...
  ...H..
  ......
  ......
  s.....

  ......
  ...TH.
  ......
  ......
  s.....

  ......
  ....TH
  ......
  ......
  s.....

  == D 1 ==

  ......
  ....T.
  .....H
  ......
  s.....

  == L 5 ==

  ......
  ....T.
  ....H.
  ......
  s.....

  ......
  ....T.
  ...H..
  ......
  s.....

  ......
  ......
  ..HT..
  ......
  s.....

  ......
  ......
  .HT...
  ......
  s.....

  ......
  ......
  HT....
  ......
  s.....

  == R 2 ==

  ......
  ......
  .H....  (H covers T)
  ......
  s.....

  ......
  ......
  .TH...
  ......
  s.....

  After simulating the rope, you can count up all of the positions the tail visited at least once. In this diagram, s again marks the starting position (which the tail also visited) and # marks other positions the tail visited:

  ..##..
  ...##.
  .####.
  ....#.
  s###..

  So, there are 13 positions the tail visited at least once.

  Simulate your complete hypothetical series of motions. How many positions does the tail of the rope visit at least once?
  """
  defmodule Rope do
    defstruct [:head, :tail, :tail_positions]

    def new(origin \\ {0, 0}) do
      %__MODULE__{
        head: origin,
        tail: origin,
        tail_positions: MapSet.new([origin])
      }
    end

    def move(rope, _direction, 0), do: rope

    def move(rope, direction, distance) do
      delta = direction_delta(direction)

      rope
      |> move_head(delta)
      |> maybe_move_tail()
      |> move(direction, distance - 1)
    end

    def direction_delta(:right), do: {1, 0}
    def direction_delta(:left), do: {-1, 0}
    def direction_delta(:up), do: {0, 1}
    def direction_delta(:down), do: {0, -1}

    def unique_tail_positions(rope) do
      MapSet.size(rope.tail_positions)
    end

    defp move_head(rope, delta) do
      %{rope | head: add(rope.head, delta)}
    end

    defp maybe_move_tail(rope) do
      {hx, hy} = rope.head
      {tx, ty} = rope.tail

      if abs(hx - tx) >= 2 or abs(hy - ty) >= 2 do
        move_tail(rope)
      else
        rope
      end
    end

    defp move_tail(rope) do
      new_tail = add(rope.tail, vector(rope.head, rope.tail))
      new_positions = MapSet.put(rope.tail_positions, new_tail)

      %{rope | tail: new_tail, tail_positions: new_positions}
    end

    defp vector({x1, y1}, {x2, y2}) do
      {unity(x1 - x2), unity(y1 - y2)}
    end

    defp unity(0), do: 0
    defp unity(x), do: div(x, abs(x))

    defp add({x1, y1}, {x2, y2}) do
      {x1 + x2, y1 + y2}
    end
  end

  def unique_tail_positions(input) do
    rope = Rope.new

    input
    |> parse()
    |> Enum.reduce(rope, &move/2)
    |> Rope.unique_tail_positions()
  end

  defp parse(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&parse_line/1)
  end

  defp parse_line("R "<> d), do: {:right, String.to_integer(d)}
  defp parse_line("L "<> d), do: {:left, String.to_integer(d)}
  defp parse_line("D "<> d), do: {:down, String.to_integer(d)}
  defp parse_line("U "<> d), do: {:up, String.to_integer(d)}

  defp move({direction, distance}, rope), do: Rope.move(rope, direction, distance)
end
